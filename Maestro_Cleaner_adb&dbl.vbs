
'-------------------------Скрипт очистки папки с проектами Maestro от файлов ADB и DBL---------------------------
'Версия 1.0
'Версия 2.0		Поиск и удаление файлов adb и dbl во всей иерархии подкаталогов.
'Версия 2.1		Исправлена ошибка в процедуре CheckSubFolders.
'Версия 2.2		Исправлена ошибка: файлы првоеряются только в подкаталогах.
'Версия 2.3		Добавлена возможность сохранять отчет об удаленных файлах.

'================================================================================================================

'!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
'!!! ПРОВЕРЬТЕ ПАРАМЕТРЫ !!!!!!!!!!!!!!!!!!!!!!!!!!!ПРОВЕРЬТЕ ПАРАМЕТРЫ!!!!!!!!!!!!!!!!!!!!ПРОВЕРЬТЕ ПАРАМЕТРЫ!!!
'!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


path_Maestro_Project = "C:\Program Files (x86)\Scm Group\Maestro\Projects"	'Каталог с проектами Maestro

filter1="_adb."									'Файлы с какими символами удаляем
filter2="_dbl."									'Файлы с какими символами удаляем	

create_log = FALSE								'Создать файл отчета (лог)? Впишите "TRUE" для создания файла отчета
path_log = "C:\Program Files (x86)\Scm Group\Maestro\Projects\cleaner_log.txt"	'Путь к файлу отчета

'!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
'!!! ПРОВЕРЬТЕ ПАРАМЕТРЫ !!!!!!!!!!!!!!!!!!!!!!!!!!!ПРОВЕРЬТЕ ПАРАМЕТРЫ!!!!!!!!!!!!!!!!!!!!ПРОВЕРЬТЕ ПАРАМЕТРЫ!!!
'!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!




















































'===========================================================================
Set FSO = CreateObject("Scripting.FileSystemObject")

'--------------------------------------------------------------------------------Процедура проверки соответствий файла фильтрам и удаления файла
Sub check_filter_and_delete (FilePath)								
	if InStr(1,FilePath,filter1,1) or InStr(1,FilePath,filter2,1) Then	'Проверка соответствия фильтрам

		if create_log = TRUE Then
			log_file.WriteLine(date & " " & time & " " & FilePath)	'Запись в лог полного пути к файлу, если переменная "create_log = TRUE"
		End if

		Set File = FSO.GetFile(FilePath)
		File.Delete							'Удаление файла
	End if
End Sub
'-------------------------------------------------------------------------------------------------------------------------------------------------------------------


'--------------------------------------------------------------------------------Проверка существования каталога с проектами Maestro
if FSO.FolderExists(path_Maestro_Project) = FALSE Then				
	MsgBox "Не существует каталог:" & vbNewLine & _
		path_Maestro_Project & vbNewLine & _
		vbNewLine & "Проверьте параметр:" & vbNewLine & _
		"path_Maestro_Project",_
		48,"Очистка папки с проектами Maestro от файлов adb и dbl"	'Вывод сообщения об отсутствии каталога, заданного переменной "path_Maestro_Project"
	WScript.Quit								'Завершение скрипта, если каталог не существует
End if
'-------------------------------------------------------------------------------------------------------------------------------------------------------------------


'--------------------------------------------------------------------------------Создание файла лога
if create_log = TRUE Then
	Set log_file = FSO.OpenTextFile(path_log,8,True)			
End if
'-------------------------------------------------------------------------------------------------------------------------------------------------------------------


'-------------------------------------------------------------------------------------------------------------------------------------------------------------------
Set Folder = FSO.GetFolder(path_Maestro_Project)				
For Each FilePath In Folder.Files						'Цикл обработки файлов в каталоге "Projects"
	check_filter_and_delete(FilePath)					'Обращение к функции проверки и удаления файла
Next
'-------------------------------------------------------------------------------------------------------------------------------------------------------------------


'-------------------------------------------------------------------------------------------------------------------------------------------------------------------
CheckSubFolders FSO.GetFolder(path_Maestro_Project)

Sub CheckSubFolders(Folder)							'Процедура обработки каталога
	For Each subfolder In Folder.SubFolders					'Цикл обработки подкаталогов
		'WScript.Echo subfolder
		For Each FilePath In subfolder.Files				'Цикл обработки файлов в каталоге
			check_filter_and_delete(FilePath)			'Обращение к функции проверки и удаления файла
		Next

		CheckSubFolders subfolder					'Вызов процедуры обработки подкаталога текущего каталога

	Next
End Sub